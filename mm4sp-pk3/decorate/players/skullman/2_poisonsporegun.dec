actor Skull_WoodWep : BaseMM8BDMWep
{
Dropitem ""
inventory.icon "SKUI2"
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Tag "Poison Sporegun"
Inventory.Pickupmessage "Power up! Poison Sporegun!"
weapon.ammotype "PoisonSporegunAmmo"
+INVENTORY.UNDROPPABLE
States
{
Spawn:
SKU2 D 1
loop
Ready:
SKU2 A 0 ACS_ExecuteAlways(998,0,2)
SKU2 A 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
SKU2 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
SKU2 A 1 A_Raise
Loop
Fire:
SKU2 A 0 A_JumpIfNoAmmo("NoAmmo")
SKU2 A 0 A_PlaySoundEx("weapon/poisonsporegun","Weapon")
SKU2 A 0 A_FireCustomMissile("PoisonSporegun",0,1,8,0)
SKU2 BC 6
SKU2 A 12
SKU2 B 0 A_Refire
goto Ready+1
NoAmmo:
SKU2 A 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor PoisonSporegunAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor PoisonSporegun 
{
PROJECTILE
+EXPLODEONWATER
+MTHRUSPECIES
Speed 25
Damage 0
damagetype "LeafShield"
Scale 2.5
Height 10
Radius 8
States
{
Spawn:
DRIL A 0
SKU2 EEFF 2 A_SpawnItemEx("PoisonSporegunFX")
loop
Death:
DRIL A 0 A_Stop
DRIL A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
DRIL A 0 A_SpawnItemEx("PoisonSpore",8,0,0,2,0,16,0,SXF_NOCHECKPOSITION)
DRIL A 0 A_SpawnItemEx("PoisonSpore",8,0,0,2,0,16,72,SXF_NOCHECKPOSITION)
DRIL A 0 A_SpawnItemEx("PoisonSpore",8,0,0,2,0,16,144,SXF_NOCHECKPOSITION)
DRIL A 0 A_SpawnItemEx("PoisonSpore",8,0,0,2,0,16,216,SXF_NOCHECKPOSITION)
DRIL A 0 A_SpawnItemEx("PoisonSpore",8,0,0,2,0,16,288,SXF_NOCHECKPOSITION)
TNT1 A 2 A_SpawnItem("DrillExplode")
stop
}
}

actor PoisonSporegunFX : BasicClientSide
{
Scale 2.0
States
{
Spawn:
TNT1 A 1
DRI2 DEFGH 2 //ignore the reused drill sprites
stop
}
}

actor PoisonSpore : BasicBouncer
{
PROJECTILE
Damage (0)
+MTHRUSPECIES
-BOUNCEONFLOORS
+USEBOUNCESTATE
+HITTRACER
damagetype "LeafShield"
Height 8
Radius 8
-NOGRAVITY
Speed 0
scale 2.5
var int user_height;
var int user_radius;
var int user_angle;
States
{
Spawn:
SKU2 J 0
SKU2 J 1 A_JumpIf(velz<=0,"Spawn2")
loop
Bounce.Ceiling:
SKU2 H 0 A_ChangeFlag("USEBOUNCESTATE",0)
SKU2 H 0 A_ChangeFlag("BOUNCEONCEILINGS",0)
SKU2 H 0 A_ChangeVelocity(momx/2,momy/2,0,2)
goto Spawn2
Spawn2:
SKU2 G 0 A_ChangeVelocity(momx/2,momy/2,momz,2)
SKU2 G 0 A_SetGravity(0.1)
Spawn2Loop:
SKU2 GHIH 8
loop
XDeath:
Crash:
SKU2 H 0 A_ChangeFlag("NOCLIP",1)
SKU2 H 0 A_ChangeFlag("NOINTERACTION",1)
SKU2 H 0 A_SetUserVar(user_height,random(16,CallACS("4SP_GetEnemyStats",1)))
SKU2 H 0 A_SetUserVar(user_radius,CallACS("4SP_GetEnemyStats",0))
SKU2 H 0 A_SetUserVar(user_angle,random(0,360))
DeathLoop:
SKU2 H 0 A_JumpIfInventory("CutterFlag",10,"Death")
SKU2 H 0 A_JumpIfInventory("IsDead",1,"Death",AAPTR_TRACER)
SKU2 H 0 A_Explode(2,28,0,0,28)
SKU2 H 1 A_GiveInventory("CutterFlag",1)
SKU2 H 1 A_Warp(AAPTR_TRACER,user_radius,0,user_height,user_angle,WARPF_NOCHECKPOSITION,"Continue")
SKU2 H 1 A_JumpIf(true,"Death")
wait
Continue:
SKU2 H 1 A_JumpIfInventory("IsDead",1,"Death",AAPTR_TRACER)
SKU2 H 1 A_Warp(AAPTR_TRACER,user_radius,0,user_height,user_angle,WARPF_NOCHECKPOSITION,"Continue2")
SKU2 H 1 A_JumpIf(true,"Death")
wait
Continue2:
SKU2 H 1 A_JumpIfInventory("IsDead",1,"Death",AAPTR_TRACER)
SKU2 H 1 A_Warp(AAPTR_TRACER,user_radius,0,user_height,user_angle,WARPF_NOCHECKPOSITION,"Continue3")
SKU2 H 1 A_JumpIf(true,"Death")
wait
Continue3:
SKU2 J 1 A_JumpIfInventory("IsDead",1,"Death",AAPTR_TRACER)
SKU2 J 1 A_Warp(AAPTR_TRACER,user_radius,0,user_height,user_angle,WARPF_NOCHECKPOSITION,"Continue4")
SKU2 J 1 A_JumpIf(true,"Death")
wait
Continue4:
SKU2 J 1 A_JumpIfInventory("IsDead",1,"Death",AAPTR_TRACER)
SKU2 J 1 A_Warp(AAPTR_TRACER,user_radius,0,user_height,user_angle,WARPF_NOCHECKPOSITION,"Continue5")
SKU2 J 1 A_JumpIf(true,"Death")
wait
Continue5:
SKU2 J 1 A_JumpIfInventory("IsDead",1,"Death",AAPTR_TRACER)
SKU2 J 1 A_Warp(AAPTR_TRACER,user_radius,0,user_height,user_angle,WARPF_NOCHECKPOSITION,"DeathLoop")
SKU2 J 1 A_JumpIf(true,"Death")
wait
Death:
TNT1 A 2
stop
}
}