actor MegaManWep2 : BaseMM8BDMWep
{
Dropitem ""
inventory.icon "EDDIEC"
Weapon.AmmoUse 7
Weapon.AmmoGive 28
Weapon.SlotNumber 0
Tag "Eddie Call"
Inventory.Pickupmessage "Power up! Eddie Call!"
weapon.ammotype "MegamanAmmo2"
+INVENTORY.UNDROPPABLE
States
{
Spawn:
TNT1 A 1
stop
Ready:
EDIC B 0 ACS_ExecuteAlways(998,0,980)
EDIC B 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
EDIC B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
EDIC B 1 A_Raise
Loop
Fire:
EDIC B 0 A_JumpIfInventory("NoMoreEddie",1,"Nope")
EDIC B 0 A_JumpIfNoAmmo("NoAmmo")
EDIC B 0 A_PlaySoundEx("item/refill","Weapon")
EDIC B 0 A_GiveInventory("NoMoreEddie",1)
EDIC B 0 A_TakeInventory("MegamanAmmo2",7)
EDIC B 0 A_SpawnItemEx("EddieTeleportSP", 80, 0, 256, 0, 0, -24, 0, SXF_NOCHECKPOSITION, 0)
EDIC CD 3
EDIC B 20
EDIC B 0 A_Refire
goto Ready+1
NoAmmo:
EDIC B 1 ACS_Execute(979,0)
Goto Ready+1
Nope:
EDIC B 1
EDIC B 0 A_Refire
Goto Ready+1
}
}

actor MegamanAmmo2 : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor NoMoreEddie : Once {}

actor EddieTeleportSP
{
Radius 8
Height 8
scale 2.5
+NOINTERACTION
PROJECTILE
+DONTBLAST
+SKYEXPLODE
states
{
Spawn:
RUSH D 0
RUSH D 9
RUSH D 0 A_ChangeFlag("NOINTERACTION", 0)
RUSH DDDDDDDDDD 1 A_CheckFloor("Next")
Goto Next
Next:
RUSH D 0 //A_ChangeFlag(NOGRAVITY, false)
Goto Death
Death:
TNT1 A 0 A_SpawnItemEx("EddieSP",0,0,3,0,0,momz,0,SXF_NOCHECKPOSITION,0) 
stop
}
}

actor EddieSP
{
-SOLID
+DONTBLAST
+NOCLIP
+SKYEXPLODE
+MISSILE
+DONTBLAST
-ISMONSTER
scale 2.5
Radius 24
Height 32
speed 6
meleerange 80
reactiontime 250
states
{
Spawn:
RUSH D 0 A_Jump(256,"Death")
RUSH D 0 A_ChangeFlag("MISSILE",0)
RUSH D 0 A_ChangeVelocity(0, 0, -32, CVF_REPLACE)
RUSH D 1 A_JumpIf(z-floorz<=0, "Search")
Goto Spawn+2
Search:
EDDI A 0 A_ChangeFlag("NOCLIP",0)
EDDI A 3
goto Melee
Melee:
EDDI E 4 A_FaceTarget
EDDI F 18 A_SpawnItemEx("RandomDrop",0,0,32,0,0,0)
EDDI F 0 A_Die
Goto Death
Death:
RUSH D 0 A_TakeFromTarget("NoMoreEddie",999)
DeathLoop:
RUSH D 0 ThrustThingZ(0, 34, 0, 1)
RUSH D 0 A_ChangeFlag("NOGRAVITY", 1)
RUSH D 1 A_JumpIf(z-ceilingz==0-32, "Bye")
Goto DeathLoop+2
Bye:
RUSH D 1
stop
}
}

actor RandomDrop
{
+NOGRAVITY
+NOINTERACTION
-SOLID
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Jump(4, "YashichiDrop")
TNT1 A 0 A_Jump(64, "BigHealthDrop", "BigWeaponDrop")
TNT1 A 0 A_Jump(128, "SmallHealthDrop", "SmallWeaponDrop")
TNT1 A 1 A_Jump(256, "SmallHealthDrop")
wait
SmallHealthDrop:
TNT1 A 1 A_SpawnItemEx("SmallHealthSP",0,0,0,4,0,5)
stop
SmallWeaponDrop:
TNT1 A 1 A_SpawnItemEx("WeaponEnergy",0,0,0,4,0,5)
stop
BigHealthDrop:
TNT1 A 1 A_SpawnItemEx("BigHealthSP",0,0,0,4,0,5)
stop
BigWeaponDrop:
TNT1 A 1 A_SpawnItemEx("BigWeaponEnergy",0,0,0,4,0,5)
stop
YashichiDrop:
TNT1 A 0 A_JumpIf(CallACS("4SP_SurvivalCheck",0)==1,"1UpDrop")
TNT1 A 1 A_SpawnItemEx("YashichiPickup",0,0,0,4,0,5)
stop
1UpDrop:
TNT1 A 1 A_SpawnItemEx("1UpPickup",0,0,0,4,0,5)
stop
}
}