actor DustManWep : BaseMM8BDMWep
{
Dropitem ""
inventory.icon "NULLICON"
Weapon.AmmoUse 1
Weapon.AmmoGive 3
Weapon.SlotNumber 1
Tag "Dust Crusher"
Inventory.Pickupmessage "Power up! Taking out the trash!"
weapon.ammotype "DustManAmmo"
+WEAPON.NOAUTOFIRE
States
{
Spawn:
WEAP X 1
loop
Ready:
DUS1 A 0 ACS_ExecuteAlways(998,0,785)
DUS1 A 0 A_JumpIfInventory("DustCharge",84,"Ready3")
DUS1 A 0 A_GiveInventory("DustCharge",1)
DUS1 A 0 A_JumpIfInventory("DustCharge",84,"Ready2")
DUS1 A 1 A_WeaponReady
Goto Ready+2
Ready2:
DUS1 D 0 A_PlaySoundEx("item/JunkArmor","SoundSlot7")
Ready3:
DUS1 D 1 A_WeaponReady
Goto Ready3
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
DUS1 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
DUS1 A 1 A_Raise
Loop
Fire:
DUS1 A 0 A_JumpIfNoAmmo("NoAmmo")
DUS1 A 0 A_JumpIfInventory("DustCharge",84,"Fire3")
DUS1 A 0 A_JumpIfInventory("DustCharge",42,"Fire2")
DUS1 A 0 A_PlaySoundEx("weapon/dustshot","Weapon")
DUS1 A 0 A_FireCustomMissile("DustBitted",0,0,8,0)
DUS1 A 0 A_TakeInventory("DustCharge",999)
DUS1 BC 3
DUS1 A 6
DUS1 B 0 A_Refire
goto Ready+2
Fire2:
DUS1 A 0 A_PlaySoundEx("weapon/dustshot","Weapon")
DUS1 A 0 A_FireCustomMissile("DustCompacted",0,0,8,0)
DUS1 A 0 A_TakeInventory("DustCharge",999)
DUS1 BC 3
DUS1 A 6
DUS1 B 0 A_Refire
goto Ready+2
Fire3:
DUS1 A 0 A_PlaySoundEx("weapon/Shoomp","Weapon")
DUS1 A 0 A_FireCustomMissile("DustCrushed",0,0,0,32)
DUS1 A 0 A_TakeInventory("DustCharge",999)
DUS1 EFE 4
DUS1 A 6
DUS1 B 0 A_Refire
goto Ready+2
NoAmmo:
DUS1 A 1
Goto Ready+2
}
}

actor DustManAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 3
+INVENTORY.IGNORESKILL
}

actor DustCharge : Inventory
{
inventory.amount 1
inventory.maxamount 84
}

actor DustBitted
{
PROJECTILE
damagetype "Buster"
translation "93:93=87:87", "94:94=87:87", "68:68=4:4"
Speed 30
Damage (14)
radius 6
height 6
scale 2.5
States
{
Spawn:
TNT1 A 0
DUST B 1 A_Jump(256, "Option1", "Option2", "Option3")
goto Option1
Option1:
DUST B 1
loop
Option2:
DUST C 1
loop
Option3:
DUST D 1
loop
}
}

actor DustCompacted : DustBitted
{
Damage (28)
radius 8
height 8
States
{
Spawn:
TNT1 A 0
JSHI V 1 A_Jump(256, "Option1", "Option2")
goto Option1
Option1:
JSHI V 1
loop
Option2:
JSHI W 1
loop
}
}

actor DustCrushed : DustBitted
{
Damage (35)
radius 10
height 10
States
{
Spawn:
TNT1 A 0
DUST A 1
loop
Death:
MMFX B 0 A_SpawnItemEx("DustBitted1",0,0,0,28,0,0,45)
MMFX B 0 A_SpawnItemEx("DustBitted2",0,0,0,28,0,0,-45)
MMFX B 0 A_SpawnItemEx("DustBitted3",0,0,0,28,0,0,225)
MMFX B 0 A_SpawnItemEx("DustBitted4",0,0,0,28,0,0,-225)
MMFX BCDE 2
stop
}
}

actor DustBitted1
{
PROJECTILE
+THRUACTORS
Radius 6
Height 6
translation "93:93=87:87", "94:94=87:87", "68:68=4:4"
scale 2.5
damage (14)
damagetype "Buster"
speed 28
states
{
Spawn:
DUST B 3
DUST B 0 A_ChangeFlag(THRUACTORS, false)
DUST B 4 
loop
}
}

actor DustBitted2 : DustBitted1
{
states
{
Spawn:
DUST C 3
DUST C 0 A_ChangeFlag(THRUACTORS, false)
DUST C 4
loop
}
}

actor DustBitted3 : DustBitted1
{
states
{
Spawn:
DUST D 3
DUST D 0 A_ChangeFlag(THRUACTORS, false)
DUST D 4
loop
}
}

actor DustBitted4 : DustBitted1
{
states
{
Spawn:
DUST E 3
DUST E 0 A_ChangeFlag(THRUACTORS, false)
DUST E 4
loop
}
}