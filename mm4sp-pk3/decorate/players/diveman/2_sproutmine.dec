actor Dive_WoodWep : BaseMM8BDMWep
{
Dropitem ""
inventory.icon "DIVI2"
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Tag "Sprout Mine"
Inventory.Pickupmessage "Power up! Sprout Mine!"
weapon.ammotype "SproutMineAmmo"
+INVENTORY.UNDROPPABLE
States
{
Spawn:
DIV2 E 1
loop
Ready:
DIV2 A 0 ACS_ExecuteAlways(998,0,2)
DIV2 A 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
DIV2 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
DIV2 A 1 A_Raise
Loop
Fire:
DIV2 A 0 A_JumpIfNoAmmo("NoAmmo")
DIV2 A 0 A_PlaySoundEx("weapon/mbuster","Weapon")
DIV2 A 0 A_FireCustomMissile("SproutMine",0,1,0,0)
DIV2 A 0 A_Jump(256,"LeftBias","RightBias")
goto LeftBias
LeftBias:
DIV2 A 0 A_FireCustomMissile("SproutMine3",random(15,20),0,0,0)
DIV2 A 0 A_FireCustomMissile("SproutMine2",random(-20,-15),0,0,0)
goto Anim
RightBias:
DIV2 A 0 A_FireCustomMissile("SproutMine2",random(15,20),0,0,0)
DIV2 A 0 A_FireCustomMissile("SproutMine3",random(-20,-15),0,0,0)
goto Anim
Anim:
DIV2 BCDCB 4
DIV2 A 8
DIV2 A 0 A_Refire
goto Ready+1
NoAmmo:
DIV2 A 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor SproutMineAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor SproutMine : BasicBouncer
{
PROJECTILE
Damage (0)
+THRUACTORS
-BOUNCEONFLOORS
damagetype "LeafShield"
Height 8
Radius 16
-NOGRAVITY
Speed 20
scale 2.5
States
{
Spawn:
DIV2 A 0
DIV2 A 0 ThrustThingZ(0,20,0,1)
DIV2 FGHI 2
Goto Spawn+2
XDeath:
Crash:
TNT1 A 1
stop
Death:
TNT1 A 0 A_SpawnItemEX("SproutMineMine",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
TNT1 A 2 A_PlaySoundEx("weapon/leafshield", "Voice")
stop
}
}

actor SproutMine2 : SproutMine {speed 15}
actor SproutMine3 : SproutMine {speed 10}

actor SproutMineMine
{
PROJECTILE
+NOCLIP
Radius 16
Height 64
scale 2.5
damage (0)
damagetype "LeafShield"
speed 0
reactiontime 150
+MTHRUSPECIES
States
{
Spawn:
DIV2 JKL 2
DIV2 M 0 A_ChangeFlag("NOCLIP",0)
Goto Mine
Mine:
DIV2 M 1 A_CountDown
loop
Death:
DIV2 N 0 A_CheckCeiling("Explode")
DIV2 N 1 ThrustThingZ(0,80,0,0)
DIV2 N 0 A_CheckCeiling("Explode")
DIV2 N 1 ThrustThingZ(0,80,0,0)
DIV2 O 0 A_CheckCeiling("Explode")
DIV2 O 1 ThrustThingZ(0,80,0,0)
DIV2 O 0 A_CheckCeiling("Explode")
DIV2 O 1 ThrustThingZ(0,80,0,0)
DIV2 N 0 A_CheckCeiling("Explode")
DIV2 N 1 ThrustThingZ(0,80,0,0)
DIV2 N 0 A_CheckCeiling("Explode")
DIV2 N 1 ThrustThingZ(0,80,0,0)
goto Explode
Explode:
DIV2 M 0 A_SpawnItemEx("SproutExplosion")
stop
}
}

actor SproutExplosion
{
damagetype "LeafShield"
+MTHRUSPECIES
+NOGRAVITY
-SOLID
+MISSILE
Radius 1
Height 1
scale 2.5
speed 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
TNT1 D 0 A_Explode(30, 128, 0, 32)
NAPA EFGHIJKLMNOPQRS 1
stop
}
}