actor DiveManWep : BaseMM8BDMWep
{
Dropitem ""
inventory.icon "NULLICON"
Weapon.AmmoUse 1
Weapon.AmmoGive 2
Weapon.SlotNumber 1
Tag "Dive Missile"
Inventory.Pickupmessage "Power up! In the navy!"
weapon.ammotype "DiveManAmmo"
+WEAPON.NOAUTOFIRE
States
{
Spawn:
WEA2 S 1
loop
Ready:
DIV1 A 0 ACS_ExecuteAlways(998,0,786)
DIV1 A 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
DIV1 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
DIV1 A 1 A_Raise
Loop
Fire:
DIV1 A 0 //A_JumpIfNoAmmo("NoAmmo")
DIV1 A 0 A_JumpIfInventory("FiredFirstMissile",1,"Fire2")
DIV1 A 0 A_PlaySoundEx("weapon/mbuster","Weapon")
DIV1 A 0 A_FireCustomMissile("SPDiveMissile",0,1,0,0)
DIV1 A 0 A_GiveInventory("FiredFirstMissile",1)
DIV1 A 0 A_SpawnItemEx("DiveCounterHelper")
DIV1 BCDCB 3
DIV1 A 4
DIV1 A 0 A_Refire
goto Ready+1
Fire2:
DIV1 A 0 A_JumpIfInventory("FiredSecondMissile",1,"NoAmmo")
DIV1 A 0 A_PlaySoundEx("weapon/mbuster","Weapon")
DIV1 A 0 A_FireCustomMissile("SPDiveMissile2",0,1,0,0)
DIV1 A 0 A_GiveInventory("FiredSecondMissile",1)
DIV1 A 0 A_SpawnItemEx("DiveCounterHelper2")
DIV1 BCDCB 3
DIV1 A 4
DIV1 A 0 A_Refire
goto Ready+1
NoAmmo:
DIV1 A 1
DIV1 A 0 A_Refire
Goto Ready+1
}
}

actor DiveManAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 2
+INVENTORY.IGNORESKILL
}

actor SPDiveMissile
{
PROJECTILE
translation "192:192=4:4", "198:198=227:227"
+SEEKERMISSILE
species "Player"
+THRUSPECIES
+SCREENSEEKER
-EXTREMEDEATH
Radius 5
Height 5
scale 2.5
damage (12)
speed 25
ReactionTime 86
States
{
Spawn:
DIV1 E 0
DIV1 E 0 A_CountDown
DIV1 E 0 A_TakeFromTarget("SPDiveCounter",999)
DIV1 EEEEEE 1 A_SeekerMissile (2, 10, SMF_LOOK, 50, 10)
goto NoSeek
NoSeek:
DIV1 E 0 A_TakeFromTarget("SPDiveCounter",999)
DIV1 E 1 A_SeekerMissile (2, 10, 0, 0, 10)
loop
Death:
MMFX B 0 A_TakeFromTarget("SPDiveCounter",999)
MMFX B 0 A_GiveToTarget("DiveManAmmo",1)
MMFX B 0 A_TakeFromTarget("FiredFirstMissile",999)
MMFX BCDE 2
stop
}
}

actor SPDiveMissile2 : SPDiveMissile
{
States
{
Spawn:
DIV1 E 0
DIV1 E 0 A_CountDown
DIV1 E 0 A_TakeFromTarget("SPDiveCounter2",999)
DIV1 EEEEEE 1 A_SeekerMissile (2, 10, SMF_LOOK, 50, 10)
goto NoSeek
NoSeek:
DIV1 E 0 A_TakeFromTarget("SPDiveCounter2",999)
DIV1 E 1 A_SeekerMissile (2, 10, 0, 0, 10)
loop
Death:
MMFX B 0 A_TakeFromTarget("SPDiveCounter2",999)
MMFX B 0 A_GiveToTarget("DiveManAmmo",1)
MMFX B 0 A_TakeFromTarget("FiredSecondMissile",999)
MMFX BCDE 2
stop
}
}

actor DiveCounterHelper
{
+MISSILE
-SOLID
+NOCLIP
+NOINTERACTION
+NOGRAVITY
+DONTBLAST
+DONTREFLECT
+THRUACTORS
renderstyle none
radius 2
height 2
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("SPDiveCounter",23,"Return")
TNT1 A 1 A_GiveToTarget("SPDiveCounter",1)
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,1)
TNT1 A 0 A_JumpIfInTargetInventory("FiredFirstMissile",1,"Spawn")
TNT1 A 0
stop
Return:
MMFX B 0 A_TakeFromTarget("SPDiveCounter",999)
MMFX B 0 A_GiveToTarget("DiveManAmmo",1)
MMFX B 0 A_TakeFromTarget("FiredFirstMissile",999)
stop    
}
}
actor DiveCounterHelper2 : DiveCounterHelper
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("SPDiveCounter2",23,"Return")
TNT1 A 1 A_GiveToTarget("SPDiveCounter2",1)
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,1)
TNT1 A 0 A_JumpIfInTargetInventory("FiredSecondMissile",1,"Spawn")
TNT1 A 0
stop
Return:
MMFX B 0 A_TakeFromTarget("SPDiveCounter2",999)
MMFX B 0 A_GiveToTarget("DiveManAmmo",1)
MMFX B 0 A_TakeFromTarget("FiredSecondMissile",999)
stop
}    
}

actor SPDiveCounter : Inventory
{
inventory.amount 1
inventory.maxamount 23
}
actor SPDiveCounter2 : SPDiveCounter {}

actor FiredFirstMissile : Once {}
actor FiredSecondMissile : Once {}