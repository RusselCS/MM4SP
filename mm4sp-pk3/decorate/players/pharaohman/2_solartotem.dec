actor Pharaoh_WoodWep : BaseMM8BDMWep
{
Dropitem ""
inventory.icon "PHAI2"
Weapon.AmmoUse 7
Weapon.AmmoGive 84
Weapon.SlotNumber 2
Tag "Solar Totem"
Inventory.Pickupmessage "Power up! Solar Totem!"
weapon.ammotype "SolarTotemAmmo"
+INVENTORY.UNDROPPABLE
States
{
Spawn:
PHA2 J 1
loop
Ready:
PHA2 A 0 ACS_ExecuteAlways(998,0,2)
PHA2 A 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
PHA2 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
PHA2 A 1 A_Raise
Loop
Fire:
PHA2 A 0 A_JumpIfNoAmmo("NoAmmo")
PHA2 A 0 A_PlaySoundEx("weapon/solartotem","Weapon")
PHA2 A 0 A_FireCustomMissile("SolarTotem",0,1,8,-8)
PHA2 BCDEFG 2 
PHA2 A 4
PHA2 A 0 A_Refire
goto Ready+1
NoAmmo:
PHA2 A 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor SolarTotemAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 84
+INVENTORY.IGNORESKILL
}

actor SolarTotem : BasicBouncer
{
PROJECTILE
+FLOATBOB
+MTHRUSPECIES
+NOTARGETSWITCH
Speed 10
reactiontime 6
Scale 2.5
Height 32
Radius 10
States
{
Spawn:
PHA2 K 0
PHA2 KKKKLLLL 1 A_SetAngle(angle+10)
PHA2 K 0 A_Countdown
loop
Death:
PHA2 M 0 A_Stop
PHA2 MMMMNNNNN 1 A_SetAngle(angle+10)
PHA2 M 0 A_PlaySoundEx("weapon/pharaoh2","Weapon")
CBST B 0 A_Explode(8, 128, 0, 0, 48)
TNT1 A 2 A_SpawnItemEx("SolarTotemPillar",0,0,floorz-z,0,0,0,0,SXF_TRANSFERPOINTERS)
stop
}
}

actor SolarTotemPillar
{
+MTHRUSPECIES
+NOGRAVITY
+FORCEYBILLBOARD
+NOTARGETSWITCH
-SOLID
scale 2.5
height 0
radius 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIf(ceilingz-z<20, 2)
TNT1 A 0 A_SpawnItemEx("SolarTotemPillar", 0, 0, 20,0,0,0,0,SXF_TRANSFERPOINTERS)
CBST B 0 A_Explode(8, 128, 0, 0, 48)
PHA2 XWVUTSRQPOPQRSTUVWX 1
stop
}
}