actor SkullManWep2 : BaseMM8BDMWep
{
Dropitem ""
inventory.icon "SKLBSI"
Weapon.AmmoUse 7
Weapon.AmmoGive 28
Weapon.SlotNumber 0
Tag "Skull Barrier"
Inventory.Pickupmessage "Power up! To the grave..."
weapon.ammotype "SkullManAmmo2"
+WEAPON.NOAUTOFIRE
States
{
Spawn:
WEA2 O 1
loop
Ready:
SKU1 D 0 ACS_ExecuteAlways(998,0,787)
SKU1 D 0 A_JumpIfInventory("SkullShieldCheck", 1, "ShieldActivateSwitch")
SKU1 D 1 A_WeaponReady
Goto Ready+1
ShieldActivateSwitch:
SKU1 EF 2 A_WeaponReady(WRF_NOFIRE)
SKU1 G 5 A_WeaponReady(WRF_NOFIRE)
goto ShieldActive
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
SKU1 D 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
SKU1 D 1 A_Raise
Loop
Fire:
SKU1 D 0 A_JumpIfInventory("SkullBarrierCooldown", 1, "NoAmmoNoSound")
SKU1 D 0 A_JumpIfInventory("SkullShieldCheck", 1, "ShieldActive")
SKU1 D 0 A_JumpIfInventory("SkullBarrierProtection", 1, "NoAmmo")
SKU1 D 0 A_JumpIfNoAmmo("NoAmmo")
SKU1 D 0 A_GiveInventory("SkullBarrierUsed",1)
SKU1 D 0 A_GiveInventory("SkullShieldCheck", 130)
SKU1 D 0 A_TakeInventory("SkullManAmmo2", 7)
SKU1 D 0 A_SpawnItemEx("SkullSkullBarrierWatcher")
SKU1 D 0 A_SpawnItemEx("SkullSkullBarrierWarper")
SKU1 D 0 A_SpawnItemEx("SkullSkullBarrierSoundHelper")
SKU1 D 0 ACS_ExecuteAlways(982, 0, 3) // protection script
goto ShieldActivate
ShieldActivate:
SKU1 EF 2
SKU1 G 5
SKU1 G 0 A_TakeInventory("SkullBarrierUsed",999)
ShieldActive:
SKU1 G 1 A_WeaponReady
SKU1 G 0 A_JumpIfInventory("SkullShieldCheck", 1, "ShieldActive")
Goto ShieldEnd
ShieldEnd:
SKU1 G 0 A_TakeInventory("SkullShieldCheck", 999)
SKU1 FFEEDD 1
SKU1 D 4
Goto Ready+1
NoAmmo:
SKU1 D 1 ACS_Execute(979,0)
Goto Ready+1
NoAmmoNoSound:
SKU1 D 1 
Goto Ready+1
}
}

actor SkullBarrierUsed : Once {}

actor SkullManAmmo2 : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor SkullSkullBarrierSoundHelper
{
+MISSILE
-SOLID
+NOCLIP
+NOINTERACTION
+NOGRAVITY
+DONTBLAST
+DONTREFLECT
+THRUACTORS
renderstyle none
radius 2
height 2
States
{
Spawn:
TNT1 A 0
TNT1 A 12 A_GiveToTarget("SkullBuzzSound",1)
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,1)
TNT1 A 0 A_JumpIfInTargetInventory("SkullShieldCheck",1,"Spawn")
TNT1 A 0
stop
}
}

actor SkullBuzzSound : CustomInventory
{
+INVENTORY.AUTOACTIVATE
States
{
Use:
TNT1 A 0 A_PlaySoundEx("weapon/skullshieldbuzz", "SoundSlot7")
stop
}
}

actor SkullSkullBarrierWatcher
{
+MISSILE
+NOINTERACTION
+DONTBLAST
+NOGRAVITY
-SOLID
Height 1
Radius 1
var int user_countdown;
States
{
Spawn:
TNT1 A 0
TNT1 AA 1
TNT1 A 0 A_TakeFromTarget("SkullShieldCheck",1)
TNT1 A 0 A_SetUserVar("user_countdown", user_countdown + 1)
TNT1 A 0 A_JumpIfInTargetInventory("BasicArmor", 1, "DeathSilent")
TNT1 A 0 A_JumpIfInTargetInventory("ShieldCheck", 1, "DeathSilent")
TNT1 A 0 A_JumpIf(user_countdown >= 130, "DeathSilent")
TNT1 A 0 A_JumpIfInTargetInventory("ShieldHits", 3, "Spawn")
Goto GotHit1
GotHit1:
TNT1 A 0 A_GiveToTarget("SkullBarrierProtection", 1)
TNT1 A 0 A_GiveToTarget("SkullBreakSound", 1)
Spawn2:
TNT1 AA 1 //A_GiveToTarget("SkullBarrierFX1",1)
TNT1 A 0 A_TakeFromTarget("SkullShieldCheck",1)
TNT1 A 0 A_SetUserVar("user_countdown", user_countdown + 1)
TNT1 A 0 A_JumpIfInTargetInventory("BasicArmor", 1, "DeathSilent")
TNT1 A 0 A_JumpIfInTargetInventory("ShieldCheck", 1, "DeathSilent")
TNT1 A 0 A_JumpIf(user_countdown >= 130, "DeathSilent")
TNT1 A 0 A_JumpIfInTargetInventory("ShieldHits", 2, "Spawn2")
Goto GotHit2
GotHit2:
TNT1 A 0 A_GiveToTarget("SkullBarrierProtection", 1)
TNT1 A 0 A_GiveToTarget("SkullBreakSound", 1)
Spawn3:
TNT1 AA 1 //A_GiveToTarget("SkullBarrierFX1",1)
TNT1 A 0 A_TakeFromTarget("SkullShieldCheck",1)
TNT1 A 0 A_SetUserVar("user_countdown", user_countdown + 1)
TNT1 A 0 A_JumpIfInTargetInventory("BasicArmor", 1, "DeathSilent")
TNT1 A 0 A_JumpIfInTargetInventory("ShieldCheck", 1, "DeathSilent")
TNT1 A 0 A_JumpIf(user_countdown >= 130, "DeathSilent")
TNT1 A 0 A_JumpIfInTargetInventory("ShieldHits", 1, "Spawn3")
Goto Death
Death:
TNT1 A 0 A_GiveToTarget("SkullBreakSound", 1)
DeathSilent:
TNT1 A 0 A_TakeFromTarget("SkullShieldCheck", 999)
TNT1 A 0 A_TakeFromTarget("ShieldHits", 999)
TNT1 A 0 A_GiveToTarget("SkullBarrierProtection", 1)
TNT1 A 0 A_GiveToTarget("SkullBarrierCooldown", 1)
stop
End:
TNT1 A 0
TNT1 A 1 A_TakeFromTarget("SkullShieldCheck", 1)
stop
}
}

actor SkullSkullBarrierWarper : BaseShieldWarper
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("core_shieldwarper_skull", 0)
TNT1 A 0 A_Jump(256, "Shield")
Goto Shield
Shield:
SKU1 HIJKLMNO 1 A_Warp(AAPTR_TARGET, 0, 0, 0, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
loop
}
}
