#library "CCHUB"
#include "zcommon.acs"
#include "8BDMLIB.acs"

// LINESTART = name
// +1 = art
// +2 = bg
#define LINESTART 20
#define CAMSTART 30
#define SELECTSTART 40
#define FACESTART 50
#define LEVELNUM_START 500
 
// [Russel] Stage Select Array Pseudo-objects
// 0 - Boss Name (ex. "Wood Man")
// 1 - Mug Shot (ex CCMUG02)
// 2 - Boss Art (ex. CCART02)
// 3 - Boss Name Texture (ex. CCNAME02)
// 4 - Camera Texture Name (ex. CCCAM02)
// 5 - Map name
//
str boss_obj[9][8] = {
    {"Wood Man", "CCMUG02", "CCART02", "CCNAME02", "CCCAM02", "CCWOO", "WoodmanDisplay", "4STGSEL1"},
    {"Spark Man", "CCMUG03", "CCART03", "CCNAME03", "CCCAM00", "CCEX", "SparkmanDisplay", "4STGSEL2"},
    {"Napalm Man", "CCMUG05", "CCART05", "CCNAME05", "CCCAM00", "CCHUB", "NapalmmanDisplay", "4STGSEL3"},
   
    {"Yamato Man", "CCMUG06", "CCART06", "CCNAME06", "CCCAM00", "CCHUB", "YamatomanDisplay", "4STGSEL4"},
    {"", "BLACK", "CLOUINVS", "CCNAME00", "CCCAM00", "CCHUB", "", ""}, // This should always be empty
    {"Burst Man", "CCMUG07", "CCART07", "CCNAME07", "CCCAM00", "CCHUB", "BurstmanDisplay", "4STGSEL5"},
   
    {"Sword Man", "CCMUG08", "CCART08", "CCNAME08", "CCCAM00", "CCHUB", "SwordmanDisplay", "4STGSEL6"},
    {"Cold Man", "CCMUG0B", "CCART0B", "CCNAME0B", "CCCAM00", "CCHUB", "ColdmanDisplay", "4STGSEL7"},
    {"Galaxy Man", "CCMUG09", "CCART09", "CCNAME09", "CCCAM00", "CCHUB", "GalaxymanDisplay", "4STGSEL8"}
};
 
str defCam = "CCCAM00";
int firstTile;
script "4SP_DefineSelect" (void) {
 
    for(int i = 0; i < 9; i++) {
 
        if(strcmp(defCam, boss_obj[i][4]) != 0) {
            SetCameraToTexture(CAMSTART+i+1, boss_obj[i][4], 120);
        }
        SetLineTexture(FACESTART+i+1, SIDE_FRONT, TEXTURE_MIDDLE, boss_obj[i][1]);
        if(strcmp("BLACK", boss_obj[i][1]) == 0) {
            firstTile = i;
        }
    }
    SetLineTexture(LINESTART, SIDE_FRONT, TEXTURE_MIDDLE, "CCNAME00");
    SetLineTexture(LINESTART+1, SIDE_FRONT, TEXTURE_MIDDLE, "CLOUINVS");
    SetLineTexture(LINESTART+2, SIDE_FRONT, TEXTURE_MIDDLE, "CCCAM00");
}
 
script "4SP_StageSelect" (void) {
 
    int tid = ActivatorTID();
    int pln = PlayerNumber();
 
    int buttons = GetPlayerInput(pln, INPUT_BUTTONS);
    int oldButtons = 0;
    int ended = false;
    int select = 4;
    int oldSelect = 0;
    int menuDelay = 0;
    
    int lineAlpha = 0;
    int lineMax = 255;

    int stageSelection = 0;

    ACS_NamedExecuteAlways("core_freezeplayer", 0, 0, 1);
    GiveInventory("NoHud", 1);

    If(CheckActorClass(tid,"MegamanSP")){SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG40");}
    If(CheckActorClass(tid,"BrightmanSP")){SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG41");}
    If(CheckActorClass(tid,"ToadmanSP"))   
    {
    int toadrandom = random(1,32);
    if(toadrandom==2){SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG4X");}//lucky lucky number 2
    else{SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG42");}
    }
    If(CheckActorClass(tid,"DrillmanSP")){SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG43");}
    If(CheckActorClass(tid,"PharaohmanSP")){SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG44");}
    If(CheckActorClass(tid,"RingmanSP")){SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG45");}
    If(CheckActorClass(tid,"DustmanSP")){SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG46");}
    If(CheckActorClass(tid,"DivemanSP")){SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG47");}
    If(CheckActorClass(tid,"SkullmanSP")){SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "CCMUG48");}
 
    while(!ended) {

        if(menuDelay > 0) {
            menuDelay--;
        }
 
        if(buttons & BT_FORWARD && menuDelay == 0) {
            menuDelay = 4;
            select = wrapAround(select - 3, 9);
        }

        if(buttons & BT_BACK && menuDelay == 0) {
            menuDelay = 4;
            select = wrapAround(select + 3, 9);
        }

        if((buttons & BT_MOVERIGHT || (buttons & BT_RIGHT)) && menuDelay == 0) {
            menuDelay = 4;
            select = wrapAround(select + 1, 9);
        }

        if((buttons & BT_MOVELEFT || (buttons & BT_LEFT)) && menuDelay == 0) {
            menuDelay = 4;
            select = wrapAround(select - 1, 9);
        }

        if(select != oldSelect) {
            SetLineTexture(SELECTSTART+oldSelect+1, SIDE_FRONT, TEXTURE_MIDDLE, "4WINDOW1");
            SetLineTexture(SELECTSTART+select+1, SIDE_FRONT, TEXTURE_MIDDLE, "4WINDOW2");
            SetLineTexture(LINESTART, SIDE_FRONT, TEXTURE_MIDDLE, boss_obj[select][3]);
            SetLineTexture(LINESTART+1, SIDE_FRONT, TEXTURE_MIDDLE, boss_obj[select][2]); // art
            SetLineTexture(LINESTART+2, SIDE_FRONT, TEXTURE_MIDDLE, boss_obj[select][4]); // bg
            lineAlpha = lineMax;
            oldSelect = select;
            LocalAmbientSound("menu/ccmenu", 128);
        }

        if(lineAlpha != 0) {
            lineAlpha = max(lineAlpha-lineMax/5, 0);
            TranslucentLine(LINESTART+1, lineMax-lineAlpha, false);
            Line_SetTextureOffset(LINESTART+1, lineAlpha/6<<16, NO_CHANGE, SIDE_FRONT, TEXFLAG_MIDDLE);
        }

        if(!ended && (buttons & BT_ATTACK)) {
            stageSelection = select+1;
            ended = true;
        }

        if(!ended && (buttons & BT_JUMP)) {
            ended = true;
        }
 
        oldButtons = buttons;
        buttons = GetPlayerInput(pln, INPUT_BUTTONS);
        Delay(1);
    }

    if(stageSelection == 0 || StrCmp("CCHUB", boss_obj[stageSelection-1][5]) == 0) { // Player jumped off teleporter.
        
        for(int i = 0; i < 9; i++) {
            SetLineTexture(SELECTSTART+i+1, SIDE_FRONT, TEXTURE_MIDDLE, "4WINDOW1");
        }
        ACS_NamedExecuteAlways("core_freezeplayer", 0, 0, 0);
        ChangeCamera(0, 0, 0);
        TakeInventory("NoHud", 1);
        SetLineTexture(LINESTART, SIDE_FRONT, TEXTURE_MIDDLE, "CCNAME00");
        SetLineTexture(FACESTART+firstTile+1, SIDE_FRONT, TEXTURE_MIDDLE, "BLACK"); // mugshot
        SetLineTexture(LINESTART+1, SIDE_FRONT, TEXTURE_MIDDLE, "CLOUINVS"); // art
        SetLineTexture(LINESTART+2, SIDE_FRONT, TEXTURE_MIDDLE, "CCCAM00"); // bg

    } else {

        AmbientSound("menu/ccselect", 128);
        SetMusic("",0);
        SetActivator(-1); 
        FadeTo(0.0, 0.0, 0.0, 1.0, 0.5);
        SetActivator(tid);
        Delay(15);    
        SetActivator(-1); 
        setplayerproperty(1,1,4);
        GiveInventory("NoHud",1);
        FadeTo(0.0, 0.0, 0.0, 0.0, 0.5);
        SetActivator(tid);
        SetMusic("MM4SEL");
        ChangeCamera(408, 1, 0);
        
        SetHudSize(208,176,0);
        SetFont("CSPFONT");
        HudMessageBold(s:boss_obj[stageSelection-1][0]; HUDMSG_TYPEON, 1, CR_WHITE, 104.4, 115.0, 7.0, 0.30, 0.0);

        SpawnSpotFacing(boss_obj[stageSelection-1][6], 409);
        SetLineTexture(61, SIDE_FRONT, TEXTURE_TOP, boss_obj[stageSelection-1][7]);
        SetLineTexture(61, SIDE_FRONT, TEXTURE_BOTTOM, boss_obj[stageSelection-1][7]);
        Scroll_Wall(61,0,1*65536,0,1);
        Scroll_Wall(61,0,-1*65536,0,4);

        Delay(245);
        HudMessageBold(s:boss_obj[stageSelection-1][0]; HUDMSG_FADEOUT, 1, CR_WHITE, 104.4, 115.0, 0, 0.5);
        SetActivator(-1); 
        FadeTo(0.0, 0.0, 0.0, 1.0, 0.5);
        SetActivator(tid);
        Delay(20);
        ACS_NamedExecuteAlways("core_freezeplayer", 0, 0, 0);
        SetActivator(-1); 
        setplayerproperty(1,0,4);
        TakeInventory("NoHud",999);
        SetActivator(tid);
        Delay(1);
        ChangeLevel(boss_obj[stageSelection-1][5], 0, CHANGELEVEL_NOINTERMISSION|CHANGELEVEL_RESETHEALTH|CHANGELEVEL_RESETINVENTORY);
    }
}
 
function int wrapAround(int x, int n) {
 
    while(x < 0) {
        x += n;
    }
    return x % n;
}
