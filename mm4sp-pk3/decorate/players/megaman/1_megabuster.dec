actor MegaManWep : BaseMM8BDMWep
{
Dropitem ""
inventory.icon "NULLICON"
Weapon.AmmoUse 1
Weapon.AmmoGive 3
Weapon.SlotNumber 1
Tag "Mega Buster"
Inventory.Pickupmessage "Power up! Super Fighting Robot!"
weapon.ammotype "BusterAmmo"
+INVENTORY.UNDROPPABLE
States
{
Spawn:
TNT1 A 1
stop
Ready:
BUST B 0 ACS_ExecuteAlways(998,0,0)
BUST B 0 ACS_ExecuteAlways(991,0,0)
BUST B 0 A_TakeInventory("WeaponCharge",999)
BUST B 2 A_WeaponReady
Goto Ready+3
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUST B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
BUST B 1 A_Raise
Loop
Altfire:
Fire:
BUST B 0 A_JumpIfNoAmmo("Hold")
BUST B 0 A_PlaySoundEx("weapon/mbuster","Weapon")
BUST B 0 A_FireCustomMissile("MegaShotSP",0,0,8,0)
BUST B 1 A_TakeInventory("WeaponCharge",999)
BUST CD 3
BUST B 1
BUST B 0 A_Refire
goto Ready+2
Fire2:
BUST B 0 A_TakeInventory("WeaponCharge",999)
BUST C 0 A_PlaySoundEx("weapon/minchrgshot","Weapon")
BUST B 1 ACS_ExecuteAlways(991,0,0)
BUST C 0 A_FireCustomMissile("MidShotSP",0,0,8,0)
BUST CD 3
BUST B 0 A_Refire
goto Ready+2
Fire3:
BUST B 0 A_TakeInventory("WeaponCharge",999)
BUST B 0 A_PlaySoundEx("weapon/cfire","Weapon")
BUST C 0 ACS_ExecuteAlways(991,0,0)
BUST C 0 A_FireCustomMissile("ChargeShotSP",0,0,8,0)
BUST CD 3 
BUST C 9
BUST B 0 A_Refire
goto Ready+2
Hold:
BUST B 0 A_JumpIfInventory("WeaponCharge",18,"Charge")
BUST B 1 A_GiveInventory("WeaponCharge",1)
BUST B 0 A_Refire
Goto Ready+2
Charge:
BUST B 0 A_JumpIfInventory("WeaponCharge",19,"Charge1")
BUST B 0 A_PlaySoundEx("weapon/coilstart","Weapon")
Goto Charge1
Charge1:
BUST B 0 A_JumpIfInventory("WeaponCharge",35,"Charge2Start")
CHBU H 1 ACS_ExecuteAlways(991,0,92)
CHBU H 0 A_Refire("Charge1.a")
Goto Fire2
Charge1.a:
BUST B 1 ACS_ExecuteAlways(991,0,0)
BUST B 0 A_GiveInventory("WeaponCharge",1)
CHBU H 0 A_Refire("Charge1")
Goto Fire2
Charge2Start:
BUST B 0 A_GunFlash("Sound.BusterCharge", GFF_NOEXTCHANGE)
Goto Charge2
Charge2:
BUST B 1 ACS_ExecuteAlways(991,0,93)
CHBU H 0 A_Refire("Charge2.a")
Goto Fire3
Charge2.a:
CHBU I 1 ACS_ExecuteAlways(991,0,94)
CHBU H 0 A_Refire("Charge2.b")
Goto Fire3
Charge2.b:
CHBU J 1 ACS_ExecuteAlways(991,0,0)
BUST B 0 A_JumpIfInventory("WeaponCharge", 35, 2)
BUST B 0 A_GiveInventory("WeaponCharge",1)
BUST B 0 A_Refire("Charge2")
Goto Fire3
}
}

actor MegaShotSP : MegaShot 
{
+MTHRUSPECIES
}

actor MidShotSP : MidShot
{
+MTHRUSPECIES
damagetype "Buster"
}

actor ChargeShotSP : MegaShot
{
+SKYEXPLODE
+DONTBLAST
+MTHRUSPECIES
Damage (35)
radius 25
height 25
States
{
Spawn:
MBU2 ABC 2
loop
}
}
